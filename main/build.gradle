/*
 * Copyright 2014 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'androidx.navigation.safeargs.kotlin'

project.archivesBaseName = "muzei"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('../version.properties')))

    buildFeatures {
        viewBinding = true
    }
    dexOptions.preDexLibraries true

    defaultConfig {
        applicationId "net.nurik.roman.muzei"
        minSdkVersion 19
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionName versionProps['name']
        versionCode versionProps['code'].toInteger()

        multiDexEnabled true
        wearAppUnbundled true

        buildConfigField "String", "DEVICE_TYPE", "\"android\""
    }

    signingConfigs {
        release {
            Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            Properties keyProps = new Properties()
            if (localProps['keystore.props.file'] != null) {
                keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            }
            storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
            keyAlias keyProps["alias"] ?: ""
            storePassword keyProps["storePass"] ?: ""
            keyPassword keyProps["pass"] ?: ""
        }
    }

    buildTypes {
        debug {
            versionNameSuffix " Debug"
            FirebasePerformance {
                instrumentationEnabled false
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            signingConfig signingConfigs.release
        }

        publicBeta.initWith(buildTypes.release)
        publicBeta {
            versionNameSuffix " " + versionProps['betaNumber']
        }

        publicDebug.initWith(buildTypes.publicBeta)
        publicDebug {
            debuggable true
            renderscriptDebuggable true
            versionNameSuffix " Debug " + versionProps['betaNumber']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutinesVersion"
    implementation "androidx.multidex:multidex:$multidexVersion"
    implementation "io.coil-kt:coil:$coilVersion"
    implementation "com.google.android.gms:play-services-wearable:$playServicesWearableVersion"
    implementation "com.twofortyfouram:android-plugin-api-for-locale:$localeVersion"
    implementation "com.davemorrissey.labs:subsampling-scale-image-view-androidx:$subsamplingVersion"
    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.drawerlayout:drawerlayout:$drawerLayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipeRefreshLayoutVersion"
    implementation "androidx.viewpager2:viewpager2:$viewPager2Version"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.browser:browser:$browserVersion"
    implementation "androidx.exifinterface:exifinterface:$exifInterfaceVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    implementation "com.google.android.apps.muzei:muzei-api:3.4.0"
    implementation project(':extensions')
    implementation project(':legacy-common')
    implementation project(':android-client-common')
    implementation project(':source-featured-art')
    implementation project(':source-gallery')
    implementation project(':source-single')
    implementation project(':gl-wallpaper')
}

apply plugin: 'com.google.gms.google-services'
